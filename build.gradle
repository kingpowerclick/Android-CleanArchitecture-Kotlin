apply from: 'buildsystem/ci.gradle'
apply from: 'buildsystem/dependencies.gradle'

buildscript {
    ext.kotlin_version = '1.2.51'
    ext.gradle_tools = '3.1.3'
    ext.build_tools = '27.0.3'
    ext.jacoco_version = '0.8.2-SNAPSHOT'
    ext.fabric_version = '1.25.1'
    ext.apollo_version = '1.0.0-alpha'

    ext.compile_sdk = 27
    ext.target_sdk = 27
    ext.min_sdk = 21

    ext.application_id = 'com.his'
    ext.version_name = '1.0'
    ext.version_code = 1
    ext.test_runner = 'android.support.test.runner.AndroidJUnitRunner'

    repositories {
        google()
        jcenter()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradle_tools"
        classpath "org.jacoco:org.jacoco.core:$jacoco_version"
        //noinspection DifferentKotlinGradleVersion
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "io.fabric.tools:gradle:$fabric_version"
        classpath "com.apollographql.apollo:apollo-gradle-plugin:$apollo_version"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task runUnitTests(dependsOn: [':app:testDebugUnitTest']) {
    description 'Run all unit tests'
}

task runAcceptanceTests(dependsOn: [':app:connectedAndroidTest']) {
    description 'Run all acceptance tests.'
}

task runCoverageTests(dependsOn: [':app:jacocoTestReport']) {
    description 'Run coverage tests.'
}

task deployDebug(type: Exec, dependsOn: 'app:installDebug') {
    def rootDir = project.rootDir
    def localProperties = new File(rootDir, "local.properties")
    if (localProperties.exists()) {
        Properties properties = new Properties()
        localProperties.withInputStream {
            inputStream -> properties.load(inputStream)
        }
        def sdkDir = properties.getProperty('sdk.dir')
        def adb = "$sdkDir/platform-tools/adb"
        commandLine "$adb", 'shell', 'am', 'start', '-n', 'com.his/com.his.core.navigation.RouteActivity'
    }
}
